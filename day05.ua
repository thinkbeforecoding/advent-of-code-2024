# Experimental!
📜 ← $ 47|53
    $ 97|13
    $ 97|61
    $ 97|47
    $ 75|29
    $ 61|13
    $ 75|53
    $ 29|13
    $ 97|29
    $ 53|29
    $ 61|53
    $ 97|53
    $ 61|29
    $ 47|13
    $ 75|47
    $ 97|75
    $ 47|61
    $ 75|61
    $ 47|29
    $ 75|13
    $ 53|13
    $ 
    $ 75,47,61,53,29
    $ 97,61,53,29,13
    $ 75,29,13
    $ 75,97,47,61,53
    $ 61,13,29
    $ 97,13,75,29,47
📄 ← &fras "input/day05.txt"
R ← ⊜(⊂∩⋕ °$"_|_")⊸(≠@\n) ⊙◌ °$"_\n\n_"  # Parse Rules
U ← ⊜(□ ⊜(⋕) ⊸(≠@,))⊸(≠@\n) ◌ °$"_\n\n_" # Parse Update

# Part1
V  ← /×∈⊙(⧈⊂):       # Validate
F! ← ▽⤚(≡(◇^0) ⊙¤)   # Filter
M  ← ≡(◇(⊡ ⊸(⌊÷2⧻))) # Middle
P₁ ← /+M F!V ⊃U R

# Part2
S! ← ⊏⍏ :⊙(⊞^0.). # Sort
P₂ ← /+M⍚(S!(/↥ ∈ ¤⊟))F!(¬V) ⊃(U|R|¤¤R)

[⊃P₁ P₂ 📜]
[⊃P₁ P₂ 📄]
